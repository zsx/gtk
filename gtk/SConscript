# vim: ft=python expandtab
Import('env')
import subprocess
import re

env_gtk = env.Clone(PDB = 'libgtk-2.0.pdb')

env_gtk['CPPDEFINES']=['HAVE_CONFIG_H',
                       ('G_LOG_DOMAN', '\\\"Gtk\\\"'),
                       ('GTK_LIBDIR', '\\\"$ESCAPED_PREFIX\\lib\\\"'),
                       ('GTK_DATADIR', '\\\"$ESCAPED_PREFIX\\share\\\"'),
                       ('GTK_DATA_PREFIX', '\\\"$ESCAPED_PREFIX\\\"'),
                       ('GTK_VERSION', '\\\"$GTK_VERSION\\\"'),
                       ('GTK_BINARY_VERSION', '\\\"$GTK_BINARY_VERSION\\\"'),
                       ('GTK_HOST', '\\\"i486_windows\\\"'),
                       'GTK_COMPILATION',
                       ('GTK_PRINT_BACKENDS', '\\\"file, lpr\\\"'),
                       #('GTK_PRINT_PREVIEW_COMMAND', '\\\"evince --unlink-tempfile --preview --print-settings %s %f\\\"'),
                       ('GTK_PRINT_PREVIEW_COMMAND', '\\\"evince\\\"'),
                       'GDK_DISABLE_DEPRECATED',
                       'GTK_DISABLE_DEPRECATED',
                       'GTK_FILE_SYSTEM_ENABLE_UNSUPPORTED',
                       'GTK_PRINT_BACKEND_ENABLE_UNSUPPORTED'] + env_gtk['DEBUG_CPPDEFINES']
env_gtk.Append(CPPPATH=['#', '#gtk', '#gdk-pixbuf', '#gdk'])

#env_gtk.DotIn('gtkconfig.h', 'gtkconfig.h.win32')
#env_gtk.Alias('install', env_gtk.Install('$PREFIX/lib/gtk-2.0/include', 'gtkconfig.h'))

env_gtk['DOT_SYMBOLS_FLAGS'] = '/DINCLUDE_VARIABLES /DG_OS_WIN32 /DALL_FILES'
env_gtk.DotSymbols2Def('gtk.def', 'gtk.symbols')

env_gtk.Command('gtkalias.h', 'gtk.symbols', env_gtk['PERL'] + ' gtk/makegtkalias.pl <$SOURCE >$TARGET')
env_gtk.Command('gtkaliasdef.c', 'gtk.symbols', env_gtk['PERL'] + ' gtk/makegtkalias.pl -def <$SOURCE >$TARGET')

gtk_public_h_sources = Split("\
	gtk.h			\
	gtkaboutdialog.h	\
	gtkaccelgroup.h		\
	gtkaccellabel.h		\
	gtkaccelmap.h		\
	gtkaccessible.h		\
	gtkaction.h		\
	gtkactiongroup.h	\
	gtkactivatable.h	\
	gtkadjustment.h		\
	gtkalignment.h		\
	gtkarrow.h		\
	gtkaspectframe.h	\
	gtkassistant.h		\
	gtkbbox.h		\
	gtkbin.h		\
	gtkbindings.h		\
	gtkbox.h		\
	gtkbuilder.h		\
	gtkbuildable.h		\
	gtkbutton.h		\
	gtkcalendar.h		\
	gtkcelleditable.h	\
	gtkcelllayout.h		\
	gtkcellrenderer.h	\
	gtkcellrendereraccel.h	\
	gtkcellrenderercombo.h	\
	gtkcellrendererpixbuf.h	\
	gtkcellrendererprogress.h \
	gtkcellrendererspin.h   \
	gtkcellrenderertext.h	\
	gtkcellrenderertoggle.h	\
	gtkcellview.h		\
	gtkcheckbutton.h	\
	gtkcheckmenuitem.h	\
	gtkclipboard.h		\
	gtkcolorbutton.h	\
	gtkcolorsel.h		\
	gtkcolorseldialog.h	\
	gtkcombobox.h		\
	gtkcomboboxentry.h	\
	gtkcontainer.h		\
	gtkcurve.h		\
	gtkdebug.h              \
	gtkdialog.h		\
	gtkdnd.h		\
	gtkdrawingarea.h	\
	gtkeditable.h           \
	gtkentry.h		\
	gtkentrybuffer.h	\
	gtkentrycompletion.h	\
	gtkenums.h		\
	gtkeventbox.h		\
	gtkexpander.h		\
	gtkfilechooser.h        \
	gtkfilechooserbutton.h  \
	gtkfilechooserdialog.h  \
	gtkfilechooserwidget.h  \
	gtkfilefilter.h		\
	gtkfixed.h		\
	gtkfontbutton.h		\
	gtkfontsel.h		\
	gtkframe.h		\
	gtkgamma.h		\
	gtkgc.h			\
	gtkhandlebox.h		\
	gtkhbbox.h		\
	gtkhbox.h		\
	gtkhpaned.h		\
	gtkhruler.h		\
	gtkhscale.h		\
	gtkhscrollbar.h		\
	gtkhseparator.h		\
	gtkhsv.h		\
	gtkiconfactory.h	\
	gtkicontheme.h		\
	gtkiconview.h		\
	gtkimage.h		\
	gtkimagemenuitem.h	\
	gtkimcontext.h		\
	gtkimcontextsimple.h	\
	gtkimmodule.h		\
	gtkimmulticontext.h	\
	gtkinfobar.h		\
	gtkinputdialog.h	\
	gtkinvisible.h		\
	gtkitem.h		\
	gtklabel.h		\
	gtklayout.h             \
	gtklinkbutton.h		\
	gtkliststore.h		\
	gtkmain.h		\
	gtkmenu.h		\
	gtkmenubar.h		\
	gtkmenuitem.h		\
	gtkmenushell.h		\
	gtkmenutoolbutton.h	\
	gtkmessagedialog.h	\
	gtkmisc.h		\
	gtkmodules.h		\
	gtkmountoperation.h     \
	gtknotebook.h		\
	gtkobject.h		\
	gtkorientable.h		\
	gtkpagesetup.h		\
	gtkpaned.h		\
	gtkpapersize.h		\
	gtkplug.h		\
	gtkprintcontext.h	\
	gtkprintoperation.h	\
	gtkprintoperationpreview.h	\
	gtkprintsettings.h	\
	gtkprivate.h		\
	gtkprogressbar.h	\
	gtkradioaction.h	\
	gtkradiobutton.h	\
	gtkradiomenuitem.h	\
	gtkradiotoolbutton.h	\
	gtkrange.h		\
	gtkrc.h			\
	gtkrecentaction.h	\
	gtkrecentchooser.h 	\
	gtkrecentchooserdialog.h \
	gtkrecentchoosermenu.h 	\
	gtkrecentchooserwidget.h \
	gtkrecentfilter.h 	\
	gtkrecentmanager.h 	\
	gtkruler.h		\
	gtkscale.h		\
	gtkscalebutton.h	\
	gtkscrollbar.h		\
	gtkscrolledwindow.h	\
	gtkselection.h		\
	gtkseparator.h		\
	gtkseparatormenuitem.h	\
	gtkseparatortoolitem.h	\
	gtkshow.h		\
	gtksettings.h		\
	gtksizegroup.h		\
	gtksocket.h		\
	gtkspinbutton.h		\
	gtkstatusbar.h		\
	gtkstatusicon.h		\
	gtkstock.h		\
	gtkstyle.h		\
	gtktable.h		\
	gtktearoffmenuitem.h    \
	gtktestutils.h		\
	gtktextbuffer.h		\
	gtktextbufferrichtext.h	\
	gtktextchild.h		\
	gtktextdisplay.h	\
	gtktextiter.h		\
	gtktextmark.h		\
	gtktexttag.h		\
	gtktexttagtable.h	\
	gtktextview.h		\
	gtktoggleaction.h	\
	gtktogglebutton.h	\
	gtktoggletoolbutton.h	\
	gtktoolbar.h		\
	gtktoolbutton.h		\
	gtktoolitem.h		\
	gtktoolshell.h		\
	gtktooltip.h		\
	gtktreednd.h		\
	gtktreemodel.h		\
	gtktreemodelfilter.h	\
	gtktreemodelsort.h	\
	gtktreeselection.h	\
	gtktreesortable.h	\
	gtktreestore.h		\
	gtktreeview.h		\
	gtktreeviewcolumn.h	\
	gtktypeutils.h		\
	gtkuimanager.h		\
	gtkvbbox.h		\
	gtkvbox.h		\
	gtkviewport.h		\
	gtkvolumebutton.h	\
	gtkvpaned.h		\
	gtkvruler.h		\
	gtkvscale.h		\
	gtkvscrollbar.h		\
	gtkvseparator.h		\
	gtkwidget.h		\
	gtkwindow.h")

# Installed header files without compatibility guarantees
# that are not included in gtk/gtk.h
gtk_semi_private_h_sources =    Split("\
	gtktextlayout.h")

# GTK+ C sources to build the library from
gtk_base_c_sources =  Split("          \
	gtkquery.c		\
	gtksearchengine.c	\
	gtksearchenginesimple.c	\
	fnmatch.c		\
	gtkaboutdialog.c	\
	gtkaccelgroup.c		\
	gtkaccellabel.c		\
	gtkaccelmap.c		\
	gtkaccessible.c		\
	gtkaction.c		\
	gtkactiongroup.c	\
	gtkactivatable.c	\
	gtkadjustment.c		\
	gtkalignment.c		\
	gtkarrow.c		\
	gtkaspectframe.c	\
	gtkassistant.c		\
	gtkbbox.c		\
	gtkbin.c		\
	gtkbindings.c		\
	gtkbox.c		\
	gtkbuildable.c		\
	gtkbuilder.c		\
	gtkbuilderparser.c	\
	gtkbutton.c		\
	gtkcalendar.c		\
	gtkcelleditable.c	\
	gtkcelllayout.c		\
	gtkcellrenderer.c	\
	gtkcellrendereraccel.c	\
	gtkcellrenderercombo.c	\
	gtkcellrendererpixbuf.c	\
	gtkcellrendererprogress.c \
	gtkcellrendererspin.c   \
	gtkcellrenderertext.c	\
	gtkcellrenderertoggle.c	\
	gtkcellview.c		\
	gtkcheckbutton.c	\
	gtkcheckmenuitem.c	\
	gtkcolorbutton.c	\
	gtkcolorsel.c		\
	gtkcolorseldialog.c	\
	gtkcombobox.c		\
	gtkcomboboxentry.c	\
	gtkcontainer.c		\
	gtkcurve.c		\
	gtkdialog.c		\
	gtkdrawingarea.c	\
	gtkeditable.c           \
	gtkentry.c		\
	gtkentrybuffer.c	\
	gtkentrycompletion.c	\
	gtkeventbox.c		\
	gtkexpander.c		\
	gtkfilechooser.c	\
	gtkfilechooserbutton.c	\
	gtkfilechooserdefault.c	\
	gtkfilechooserdialog.c	\
	gtkfilechooserembed.c	\
	gtkfilechooserentry.c	\
	gtkfilechoosersettings.c \
	gtkfilechooserutils.c	\
	gtkfilechooserwidget.c	\
	gtkfilefilter.c		\
	gtkfilesystem.c		\
	gtkfilesystemmodel.c	\
	gtkfixed.c		\
	gtkfontbutton.c         \
	gtkfontsel.c            \
	gtkframe.c		\
	gtkgamma.c		\
	gtkgc.c			\
	gtkhandlebox.c		\
	gtkhbbox.c		\
	gtkhbox.c		\
	gtkhpaned.c		\
	gtkhruler.c		\
	gtkhscale.c		\
	gtkhscrollbar.c		\
	gtkhseparator.c		\
	gtkhsv.c		\
	gtkiconcache.c		\
	gtkiconcachevalidator.c	\
	gtkiconfactory.c	\
	gtkicontheme.c		\
	gtkiconview.c		\
	gtkimage.c		\
	gtkimagemenuitem.c	\
	gtkimcontext.c		\
	gtkimcontextsimple.c	\
	gtkimmodule.c		\
	gtkimmulticontext.c	\
	gtkinfobar.c		\
	gtkinputdialog.c	\
	gtkinvisible.c		\
	gtkitem.c		\
	gtkkeyhash.c		\
	gtklabel.c		\
	gtklayout.c		\
	gtklinkbutton.c		\
	gtkliststore.c		\
	gtkmain.c		\
	gtkmarshal.c		\
	gtkmarshalers.c		\
	gtkmenu.c		\
	gtkmenubar.c		\
	gtkmenuitem.c		\
	gtkmenushell.c		\
	gtkmenutoolbutton.c	\
	gtkmessagedialog.c	\
	gtkmisc.c		\
	gtkmnemonichash.c	\
	gtkmodules.c		\
	gtkmountoperation.c     \
	gtknotebook.c		\
	gtkobject.c		\
	gtkorientable.c		\
	gtkpagesetup.c		\
	gtkpaned.c		\
	gtkpapersize.c		\
	gtkpathbar.c		\
	gtkplug.c		\
	gtkprintcontext.c	\
	gtkprintoperation.c	\
	gtkprintoperationpreview.c	\
	gtkprintsettings.c	\
	gtkprintutils.c		\
	gtkprogressbar.c	\
	gtkradioaction.c	\
	gtkradiobutton.c	\
	gtkradiomenuitem.c	\
	gtkradiotoolbutton.c	\
	gtkrange.c		\
	gtkrbtree.c 		\
	gtkrc.c			\
	gtkrecentaction.c	\
	gtkrecentchooserdefault.c \
	gtkrecentchooserdialog.c \
	gtkrecentchoosermenu.c 	\
	gtkrecentchooserwidget.c \
	gtkrecentchooserutils.c \
	gtkrecentchooser.c 	\
	gtkrecentfilter.c 	\
	gtkrecentmanager.c 	\
	gtkruler.c		\
	gtkscale.c		\
	gtkscalebutton.c	\
	gtkscrollbar.c		\
	gtkscrolledwindow.c	\
	gtkselection.c		\
	gtkseparator.c		\
	gtkseparatormenuitem.c	\
	gtkseparatortoolitem.c	\
	gtksettings.c		\
	gtksizegroup.c		\
	gtkshow.c		\
	gtksocket.c		\
	gtkspinbutton.c		\
	gtkstatusbar.c		\
	gtkstatusicon.c		\
	gtkstock.c		\
	gtkstyle.c		\
	gtktable.c		\
	gtktearoffmenuitem.c    \
	gtktestutils.c		\
	gtktextbtree.c		\
	gtktextbuffer.c		\
	gtktextbufferrichtext.c	\
	gtktextbufferserialize.c\
	gtktextchild.c		\
	gtktextdisplay.c	\
	gtktextiter.c		\
	gtktextlayout.c		\
	gtktextmark.c		\
	gtktextsegment.c	\
	gtktexttag.c		\
	gtktexttagtable.c	\
	gtktexttypes.c		\
	gtktextutil.c		\
	gtktextview.c		\
	gtkthemes.c		\
	gtktoggleaction.c	\
	gtktogglebutton.c	\
	gtktoggletoolbutton.c	\
	gtktoolbar.c		\
	gtktoolbutton.c		\
	gtktoolitem.c		\
	gtktoolshell.c		\
	gtktooltip.c		\
	gtktreedatalist.c	\
	gtktreednd.c		\
	gtktreemodel.c		\
	gtktreemodelfilter.c	\
	gtktreemodelsort.c	\
	gtktreeselection.c	\
	gtktreesortable.c	\
	gtktreestore.c		\
	gtktreeview.c		\
	gtktreeviewcolumn.c	\
	gtktypebuiltins.c	\
	gtktypeutils.c		\
	gtkuimanager.c		\
	gtkvbbox.c		\
	gtkvbox.c		\
	gtkvolumebutton.c	\
	gtkviewport.c		\
	gtkvpaned.c		\
	gtkvruler.c		\
	gtkvscale.c		\
	gtkvscrollbar.c		\
	gtkvseparator.c		\
	gtkwidget.c		\
	gtkwindow-decorate.c    \
	gtkwindow.c")

# Broken
gtk_public_h_sources += Split("\
	gtktext.h		\
	gtktree.h		\
	gtktreeitem.h")
gtk_base_c_sources += Split("\
	gtktext.c		\
	gtktree.c		\
	gtktreeitem.c")

# Deprecated
gtk_public_h_sources += Split("\
	gtkclist.h		\
	gtkcombo.h		\
	gtkctree.h		\
	gtkfilesel.h		\
	gtkitemfactory.h	\
	gtklist.h		\
	gtklistitem.h		\
	gtkoldeditable.h	\
	gtkoptionmenu.h		\
	gtkpixmap.h		\
	gtkpreview.h		\
	gtkprogress.h		\
	gtksignal.h		\
	gtktipsquery.h		\
	gtktooltips.h")
gtk_base_c_sources += Split("\
	gtkclist.c		\
	gtkcombo.c		\
	gtkctree.c		\
	gtkfilesel.c		\
	gtkitemfactory.c	\
	gtklist.c		\
	gtklistitem.c		\
	gtkoldeditable.c	\
	gtkoptionmenu.c		\
	gtkpixmap.c		\
	gtkpreview.c		\
	gtkprogress.c		\
	gtksignal.c		\
	gtktipsquery.c		\
	gtktooltips.c")

gtk_os_win32_c_sources = Split("\
	gtkprint-win32.c		\
	gtkprintoperation-win32.c")

gtk_use_win32_c_sources = Split("\
	gtkplug-win32.c   \
	gtksocket-win32.c \
	gtkwin32embed.c   \
	gtkwin32embedwidget.c \
	gtkmountoperation-stub.c")

gtk_clipboard_dnd_c_sources = Split("gtkclipboard.c gtkdnd.c")

# built sources that get installed with the header files
gtk_built_public_sources = Split("		\
	gtkmarshal.h				\
	gtktypebuiltins.h")

gtk_built_sources = Split(" \
	gtkaliasdef.c				\
	gtktypebuiltins.c			\
	gtkmarshalers.c				\
	gtkmarshal.c")
env_gtk.AlwaysBuild(env_gtk.Command('gtkmarshalers.h', 'gtkmarshalers.list','''
	%s\\bin\\glib-genmarshal --prefix=_gtk_marshal $SOURCE --header >> $TARGET''' % env_gtk['PREFIX']))
env_gtk.AlwaysBuild(env_gtk.Command('gtkmarshalers.c', 'gtkmarshalers.list','''
        echo #include "gtkmarshalers.h" > $TARGET
        echo #include "gtkalias.h" >> $TARGET
	%s\\bin\\glib-genmarshal --prefix=_gtk_marshal $SOURCE --body >> $TARGET
        ''' % env_gtk['PREFIX']))

env_gtk.AlwaysBuild(env_gtk.Command('gtkmarshal.h', 'gtkmarshal.list','''
	echo #ifndef GTK_DISABLE_DEPRECATED > $TARGET
        %s\\bin\\glib-genmarshal --prefix=gtk_marshal $SOURCE --header >> $TARGET
	echo #endif /* GTK_DISABLE_DEPRECATED */ >> $TARGET
        ''' % env_gtk['PREFIX']))
env_gtk.AlwaysBuild(env_gtk.Command('gtkmarshal.c', 'gtkmarshal.list','''
        echo #include "gtkmarshal.h" > $TARGET
        echo #include "gtkalias.h" >> $TARGET
	%s\\bin\\glib-genmarshal --prefix=gtk_marshal $SOURCE --body >> $TARGET
	echo #define __gtk_marshal_MARSHAL_C__ >> $TARGET
	echo #include "gtkaliasdef.c" >> $TARGET
        ''' % env_gtk['PREFIX']))

env_gtk_h = env_gtk.Clone(GLIB_MKENUMS_ARGV = [('template', r'gtk\gtktypebuiltins.h.template')])
env_gtk_h.AlwaysBuild(env_gtk_h.MkenumsGenerator('gtktypebuiltins.h', gtk_public_h_sources))

env_gtk_c = env_gtk.Clone(GLIB_MKENUMS_ARGV = [('template', r'gtk\gtktypebuiltins.c.template')])
env_gtk_c.AlwaysBuild(env_gtk_c.MkenumsGenerator('gtktypebuiltins.c', gtk_public_h_sources))


env_gtk.DotIn('gtkversion.h', 'gtkversion.h.in')

Alias('install', env_gtk.Install('$PREFIX/include/gtk-2.0/gtk', gtk_public_h_sources + gtk_semi_private_h_sources + gtk_built_public_sources + ['gtkversion.h']))

env_gtk.DotIn('gtk-win32.rc', 'gtk-win32.rc.in')
env_gtk.RES('gtk-win32.rc')
env_gtk.ParseConfig('pkg-config gobject-2.0 --cflags --libs')
env_gtk.ParseConfig('pkg-config gthread-2.0 --cflags --libs')
env_gtk.ParseConfig('pkg-config gio-2.0 --cflags --libs')
env_gtk.ParseConfig('pkg-config cairo --cflags --libs')
env_gtk.ParseConfig('pkg-config pangocairo --cflags --libs')
env_gtk.ParseConfig('pkg-config atk --cflags --libs')

def gtk_type_funcs(target, source, env):
    '''
	echo '#include <gtk/gtk.h>' > xgen-gtfsrc.c && \
	  ${CPP} $(DEFS) $(INCLUDES) -DGTK_ENABLE_BROKEN $(AM_CPPFLAGS) $(CPPFLAGS) $(AM_CFLAGS) $(CFLAGS) xgen-gtfsrc.c | \
	  grep -o '\bg[td]k_[a-zA-Z0-9_]*_get_type\b' | \
	  sort | uniq | \
	  sed '{ s/^/*tp++ = /; s/$$/();/; }' > xgen-gtf \
    '''
    src = file('gtk\\xgen-gtfsrc.c', 'w')
    src.write('#include <gtk/gtk.h>\n')
    src.close()
    cppflags = []
    for x in env['CPPPATH']:
        if x == '#':
            cppflags.append('/I.')
        elif x.startswith('#'):
            cppflags.append(x.replace('#', '/I'))
        else:
            cppflags.append('/I' + x)
    print cppflags
    cpp = subprocess.Popen([env['CC']] + Split('/nologo /EP') + cppflags + ['gtk\\xgen-gtfsrc.c'], stdout=subprocess.PIPE).communicate()[0]
    lines = cpp.split('\r\n')
    t = file(str(target[0]), 'w')
    lines2 = set()
    get_types = re.compile(r'\bg[td]k_[a-zA-Z0-9_]*_get_type\b')
    for line in lines:
        if line == '':
            continue
        else:
            mo = re.search(get_types, line)
            if mo:
                lines2.add(r'*tp ++ = ' + mo.group(0) + '();\n')
    lines2 = list(lines2)
    lines2.sort()
    t.writelines(lines2)
    t.close()

gtk_type_funcs_processor = env_gtk.Builder(action= gtk_type_funcs)
env_gtk.Append(BUILDERS={'GtkTypeFuncs': gtk_type_funcs_processor})
env_gtk_types = env_gtk.Clone()
env_gtk_types.Append(CPPDEFINES='GTK_ENABLE_BROKEN')
env_gtk_types.GtkTypeFuncs('gtktypefuncs.c', Glob('#gtk/*.h') + Glob('#gdk/*.h'))

env_gtk.Append(LIBS=['comdlg32',
                     'winspool',
                     'gdi32',
                     'comctl32',
                     'ole32',
                     'user32',
                     'advapi32',
                     'wsock32',
                     'shell32'])
SConscript('win32/SConscript', exports = 'env_gtk')
gtk_dll=env_gtk.SharedLibrary(['libgtk-win32-2.0' + env_gtk['LIB_SUFFIX'] + env_gtk['SHLIBSUFFIX'],
                               'libgtk-win32-2.0.lib'],
                               gtk_base_c_sources + gtk_os_win32_c_sources + gtk_use_win32_c_sources + gtk_built_sources + gtk_clipboard_dnd_c_sources + ['gtk.def', 'gtk-win32.res', '#gdk/libgdk-win32-2.0.lib', '#gdk-pixbuf/libgdk-pixbuf-2.0.lib'])
env_gtk.AddPostAction(gtk_dll, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')

Alias('install', env_gtk.Install('$PREFIX/bin', 'libgtk-win32-2.0' + env_gtk['LIB_SUFFIX'] + '.dll'))
Alias('install', env_gtk.Install('$PREFIX/lib', 'libgtk-win32-2.0.lib'))
Alias('install', env_gtk.InstallAs('$PREFIX/lib/gtk-win32-2.0.lib', 'libgtk-win32-2.0.lib'))
if env_gtk['DEBUG'] == 1:
    Alias('install', env_gtk.Install('$PREFIX/pdb', env_gtk['PDB']))


#
# Installed tools
#
env_gtk_query_immodule = env_gtk.Clone(PDB = 'gtk-query-immodules.pdb')
env_gtk_query_immodule.Program('gtk-query-immodules-2.0', ['queryimmodules.c', '#gdk/libgdk-win32-2.0.lib', '#gdk-pixbuf/libgdk-pixbuf-2.0.lib', 'libgtk-win32-2.0.lib'])
env_gtk_update_icon_cache = env_gtk.Clone(PDB = 'gtk-update-icon-cache.pdb')
update_icon_cache = env_gtk_update_icon_cache.Program('gtk-update-icon-cache', ['updateiconcache.c', '#gdk/libgdk-win32-2.0.lib', '#gdk-pixbuf/libgdk_pixbuf-2.0.lib'])

env_gtk.AppendENVPath('PATH', 'gdk')
env_gtk.AppendENVPath('PATH', 'gdk-pixbuf')
env_gtk.AlwaysBuild(env_gtk.Command('gtkbuiltincache.h', [], '''
    gtk\\gtk-update-icon-cache.exe --force --ignore-theme-index --source builtin_icons gtk\\stock-icons > $TARGET'''))
env_gtk.Depends('gtkbuiltincache.h', update_icon_cache)

Alias('install', env_gtk.Install('$PREFIX/bin', 'gtk-query-immodules-2.0.exe'))
Alias('install', env_gtk.Install('$PREFIX/bin', 'gtk-update-icon-cache.exe'))
Alias('install', env_gtk.Install('$PREFIX/bin', 'gtk-builder-convert'))

#
#<,>:g/\$(RM)/d
#<,>s/&& \$(LN_S)/(/g
#<,>s/ *\\$/)/g
#<,>s/png /png', '/
#<,>s/( gtk/('gtk/
#<,>s/png[\s\t]*)/png),/
#(src, dest)
subs = (('gtk-go-forward-ltr.png', 'gtk-go-back-rtl.png'),
        ('gtk-go-back-ltr.png', 'gtk-go-forward-rtl.png'),
        ('gtk-goto-last-ltr.png', 'gtk-goto-first-rtl.png'),
        ('gtk-goto-first-ltr.png', 'gtk-goto-last-rtl.png'),
        ('gtk-media-rewind-ltr.png', 'gtk-media-forward-rtl.png'),
        ('gtk-media-previous-ltr.png', 'gtk-media-next-rtl.png'),
        ('gtk-media-next-ltr.png', 'gtk-media-previous-rtl.png'),
        ('gtk-media-forward-ltr.png', 'gtk-media-rewind-rtl.png'),
        ('gtk-floppy.png', 'gtk-save.png'),
        ('gtk-harddisk.png', 'drive-harddisk.png'),
        ('gtk-directory.png', 'folder.png'),
        ('gtk-directory.png', 'folder-remote.png'),
        ('gtk-directory.png', 'user-home.png'),
        ('gtk-directory.png', 'user-desktop.png'),
        ('gtk-file.png', 'text-x-generic.png'))

for x in subs:
    for y in ['16', '24']:
        d = env_gtk.Command('stock-icons/%s/%s' % (y, x[1]), 'stock-icons/%s/%s' % (y, x[0]), 'copy $SOURCE $TARGET')
        env_gtk.Depends('gtkbuiltincache.h', d)
